apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.0"
    defaultConfig {
        applicationId "com.lyl.calendar"
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    dataBinding {
        enabled = true
    }

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
        }
        release {
            // keystore
            storeFile file('lyl.keystore')
            // keys
            Properties properties = loadProperties('app/ant.properties')

            storePassword properties.get('key.store.password')
            keyAlias properties.get('key.alias')
            keyPassword properties.get('key.alias.password')
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
            resValue "string", "app_name", "加班打卡Debug"

            jniDebuggable false
            renderscriptDebuggable false
            pseudoLocalesEnabled false
            zipAlignEnabled false
            minifyEnabled false
            shrinkResources false
        }
        release {
            debuggable false
            signingConfig signingConfigs.release
            resValue "string", "app_name", "加班打卡"

            jniDebuggable false
            renderscriptDebuggable false
            pseudoLocalesEnabled false
            zipAlignEnabled true
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:24.1.1'
    // 时间选择控件
    compile 'cn.aigestudio.wheelpicker:WheelPicker:1.1.1'
    // 数据库
    compile 'de.greenrobot:greendao:2.1.0'
    compile 'com.android.support:design:24.1.1'
    testCompile 'junit:junit:4.12'
}

Properties loadProperties(String name) {
    Properties properties = new Properties()

    InputStream is = null;
    try {
        is = new FileInputStream(name)
        properties.load(is)
    } catch (Throwable tr) {
        tr.printStackTrace()
    } finally {
        if (is != null) {
            try {
                is.close()
            } catch (Throwable tr) {
                tr.printStackTrace()
            }
        }
    }

    return properties
}
